plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

test {
    testLogging {
        events "PASSED", "SKIPPED", "FAILED", "STANDARD_OUT", "STANDARD_ERROR"
    }
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "net.java.dev.jna:jna:5.8.0"
    api "org.slf4j:slf4j-api:1.7.30"
    testImplementation "junit:junit:4.13.2"
    testImplementation "ch.qos.logback:logback-classic:1.2.3"
    testImplementation "ch.qos.logback:logback-core:1.2.3"
}

afterEvaluate {
    publishing {
        publications {

            release(MavenPublication) {
                from components.java

                groupId = 'org.bitcoindevkit'
                artifactId = 'bdk-jvm'
                version = '0.3.1'

                pom {
                    name = 'bdk-jvm'
                    description = 'Bitcoin Dev Kit Kotlin language bindings.'
                    url = "https://bitcoindevkit.org"
                    licenses {
                        license {
                            name = "APACHE"
                            url = "https://github.com/bitcoindevkit/bdk/blob/master/LICENSE-APACHE"
                        }
                        license {
                            name = "MIT"
                            url = "https://github.com/bitcoindevkit/bdk/blob/master/LICENSE-MIT"
                        }
                    }
                    developers {
                        developer {
                            id = 'notmandatory'
                            name = 'Steve Myers'
                            email = 'notmandatory@noreply.github.org'
                        }
                        developer {
                            id = 'artfuldev'
                            name = 'Sudarsan Balaji'
                            email = 'sudarsan.balaji@artfuldev.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/bitcoindevkit/bdk-ffi.git'
                        developerConnection = 'scm:git:ssh://github.com/bitcoindevkit/bdk-ffi.git'
                        url = 'https://github.com/bitcoindevkit/bdk-ffi/tree/master'
                    }
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications
}
