apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'net.java.dev.jna:jna:5.8.0@aar'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.core:core-ktx:1.7.0'
    api "org.slf4j:slf4j-api:1.7.30"

    androidTestImplementation 'com.github.tony19:logback-android:2.0.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1'
}

afterEvaluate {

    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'org.bitcoindevkit'
                artifactId = 'bdk-android'
                version = '0.4.1-SNAPSHOT'

                pom {
                    name = 'bdk-android'
                    description = 'Bitcoin Dev Kit Kotlin language bindings.'
                    url = "https://bitcoindevkit.org"
                    licenses {
                        license {
                            name = "APACHE"
                            url = "https://github.com/bitcoindevkit/bdk/blob/master/LICENSE-APACHE"
                        }
                        license {
                            name = "MIT"
                            url = "https://github.com/bitcoindevkit/bdk/blob/master/LICENSE-MIT"
                        }
                    }
                    developers {
                        developer {
                            id = 'notmandatory'
                            name = 'Steve Myers'
                            email = 'notmandatory@noreply.github.org'
                        }
                        developer {
                            id = 'artfuldev'
                            name = 'Sudarsan Balaji'
                            email = 'sudarsan.balaji@artfuldev.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/bitcoindevkit/bdk-ffi.git'
                        developerConnection = 'scm:git:ssh://github.com/bitcoindevkit/bdk-ffi.git'
                        url = 'https://github.com/bitcoindevkit/bdk-ffi/tree/master'
                    }
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications
}
