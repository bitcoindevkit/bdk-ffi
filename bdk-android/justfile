[group("Repo")]
[doc("Default command; list all available commands.")]
@list:
  just --list --unsorted

[group("Repo")]
[doc("Open repo on GitHub in your default browser.")]
repo:
  open https://github.com/bitcoindevkit/bdk-ffi

[group("Repo")]
[doc("Build the API docs.")]
docs:
  ./gradlew :lib:dokkaGeneratePublicationHtml

[group("Repo")]
[doc("Publish the library to your local Maven repository.")]
publish-local:
  ./gradlew publishToMavenLocal -P localBuild

[group("Build")]
[doc("Build the library for given ARCH.")]
build ARCH="macos-aarch64":
  bash ./scripts/release/build-release-{{ARCH}}.sh

[group("Build")]
[doc("Build the library for a single architecture in development mode.")]
build-dev:
  bash ./scripts/dev/build-dev-macos-aarch64.sh

[group("Build")]
[doc("List available architectures for the build command.")]
@list-architectures:
    echo "Available architectures:"
    echo "    - linux-x86_64"
    echo "    - macos-aarch64"
    echo "    - windows-x86_64"

[group("Build")]
[doc("Remove all caches and previous build directories to start from scratch.")]
clean:
  rm -rf ../bdk-ffi/target/
  rm -rf ./build/
  rm -rf ./lib/build/
  rm -rf ./plugins/build/

[group("Test")]
[doc("Run all tests.")]
test:
  ./gradlew connectedAndroidTest
